plugins {
    id("com.android.application")
    id("org.jetbrains.kotlin.android")
    id("org.jetbrains.kotlin.plugin.compose")
}

android {
    namespace = "io.harbor.fable"
    compileSdk = 34
    // Must match the NDK installed by the CI workflow.
    ndkVersion = "27.0.12077973"

    lint {
        // targetSdk is pinned to 28 on purpose (see defaultConfig).
        disable += "ExpiredTargetSdkVersion"
    }

    defaultConfig {
        applicationId = "io.harbor.fable"
        minSdk = 28
        // Same as Winlator ("keep targetSdkVersion at 28"): from targetSdk 29 Android forbids
        // exec() of files in the app's data directory, which is where downloaded Box64 and Wine
        // live. The linker64 workaround only covers the first hop; box64 itself then execs
        // wineserver/wine-preloader, which fails under targetSdk >= 29. Sideload-only app.
        targetSdk = 28
        versionCode = 5
        versionName = "0.4.1"
        ndk { abiFilters += listOf("arm64-v8a") }
    }

    externalNativeBuild {
        cmake {
            path = file("src/main/cpp/CMakeLists.txt")
            version = "3.22.1"
        }
    }

    buildFeatures {
        compose = true
        buildConfig = true
    }

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }

    kotlinOptions { jvmTarget = "17" }

    packaging {
        resources { excludes += "/META-INF/{AL2.0,LGPL2.1}" }
    }
}

dependencies {
    val composeBom = platform("androidx.compose:compose-bom:2024.10.01")
    implementation(composeBom)

    implementation("androidx.core:core-ktx:1.13.1")
    implementation("androidx.lifecycle:lifecycle-runtime-ktx:2.8.6")
    implementation("androidx.lifecycle:lifecycle-runtime-compose:2.8.6")
    implementation("androidx.activity:activity-compose:1.9.3")
    implementation("androidx.compose.ui:ui")
    implementation("androidx.compose.ui:ui-graphics")
    implementation("androidx.compose.material3:material3")
    implementation("androidx.compose.animation:animation")
    implementation("androidx.navigation:navigation-compose:2.8.3")
    implementation("androidx.documentfile:documentfile:1.0.1")
    implementation("org.jetbrains.kotlinx:kotlinx-coroutines-android:1.8.1")
    implementation("io.coil-kt:coil-compose:2.7.0")

    // Archive extraction for downloaded Wine / Box64 packages (.tar.xz, .tar.gz).
    implementation("org.apache.commons:commons-compress:1.27.1")
    implementation("org.tukaani:xz:1.10")
    // Winlator .wcp packages (bionic Wine, Box64) are zstd-compressed tars. Same library
    // Winlator uses; ships prebuilt arm64 JNI libs in the AAR.
    implementation("com.github.luben:zstd-jni:1.5.6-9@aar")
}
